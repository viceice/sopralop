buildscript {
    dependencies {
        classpath 'com.tmatesoft.svnkit:svnkit:1.10.13'
        classpath 'org.eclipse.jgit:org.eclipse.jgit:6.10.1.202505221210-r'
        classpath 'org.apache.sshd:sshd-core:2.16.0'
        modules {
            module('org.tmatesoft.svnkit:svnkit') {
                replacedBy("com.tmatesoft.svnkit:svnkit", "was renamed in v1.10.12")
            }
        }
    }
}

plugins {
   id 'java'
   id 'application'
   id 'net.nemerosa.versioning' version '3.1.0'
}

def main = 'info.kriese.sopra.SoPraLOP'
def title = 'Software-Praktikum Lineare Optimierung'
def author = 'Michael Kriese & Peer Sterner'
version = '2.0.0-beta.1'

repositories {
    mavenCentral()
    maven {
        url = uri('https://jogamp.org/deployment/maven/')
    }
    maven {
        url = uri('https://jogamp.org/deployment/maven-java3d/')
    }
}

dependencies {
    implementation 'org.jogamp.java3d:java3d-core:1.7.2'
    implementation 'org.jogamp.java3d:java3d-utils:1.7.2'
    implementation 'org.jogamp.java3d:vecmath:1.7.2'

    runtimeOnly 'org.jogamp.gluegen:gluegen-rt:2.6.0:natives-android-aarch64'
    runtimeOnly 'org.jogamp.gluegen:gluegen-rt:2.6.0:natives-linux-amd64'
    runtimeOnly 'org.jogamp.gluegen:gluegen-rt:2.6.0:natives-linux-armv6hf'
    runtimeOnly 'org.jogamp.gluegen:gluegen-rt:2.6.0:natives-macosx-universal'
    runtimeOnly 'org.jogamp.gluegen:gluegen-rt:2.6.0:natives-windows-amd64'

    runtimeOnly 'org.jogamp.jogl:jogl-all:2.6.0:natives-android-aarch64'
    runtimeOnly 'org.jogamp.jogl:jogl-all:2.6.0:natives-linux-amd64'
    runtimeOnly 'org.jogamp.jogl:jogl-all:2.6.0:natives-linux-armv6hf'
    runtimeOnly 'org.jogamp.jogl:jogl-all:2.6.0:natives-macosx-universal'
    runtimeOnly 'org.jogamp.jogl:jogl-all:2.6.0:natives-windows-amd64'
}

application {
    mainClass = main
}

test {
    // required for gradle v9 because we don't have tests yet
    if (GradleVersion.version('9.0.0').compareTo(GradleVersion.current()) <= 0) {
        failOnNoDiscoveredTests = false
    }
}

jar {
    manifest {
        attributes(
            'Main-Class'     : main,
            'Built-By'       : System.properties['user.name'],
            'Build-Timestamp': new java.text.SimpleDateFormat('yyyy-MM-dd\'T\'HH:mm:ss.SSSZ').format(new Date()),
            'Build-Revision' : "${versioning.info.full}",
            'Created-By'     : "Gradle ${gradle.gradleVersion}",
            'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
            'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}",

            'Specification-Title': title,
            'Specification-Vendor': author,
            'Specification-Version': '2.0',
            'Implementation-Title': title,
            'Implementation-Vendor': author,
            'Implementation-Version': version
        )
    }
}

processResources {
    with copySpec {
        from 'sopra.properties'
        filter(ReplaceTokens, tokens: [version: version, title: title, author: author])
    }
}

import org.apache.tools.ant.filters.ReplaceTokens
